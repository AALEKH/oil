eval_input: expr NEWLINE* ENDMARKER
test_input: test NEWLINE* ENDMARKER

expr: term (('+'|'-') term)*

# note: In Oil, integer division is 'div'
term: factor (('*'|'/'|'div'|'%') factor)*

factor: ('+'|'-'|'~') factor | power

power: atom trailer* ['^' factor]

trailer: '(' [arglist] ')' | '[' subscriptlist ']'

atom: NAME | NUMBER

# Trailing commas are allowed!
arglist: expr (',' expr)* [',']
subscriptlist: expr (',' expr)* [',']


test: or_test ['if' or_test 'else' test] | func_literal
or_test: and_test ('or' and_test)*
and_test: not_test ('and' not_test)*
not_test: 'not' not_test | comparison
comparison: expr (comp_op expr)*
comp_op: '<'|'>'|'=='|'>='|'<='|'!='|'in'|'not' 'in'|'is'|'is' 'not'

func_literal: 'func'
