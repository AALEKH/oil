eval_input: expr NEWLINE* ENDMARKER
test_input: test NEWLINE* ENDMARKER

array_input: array_literal NEWLINE* ENDMARKER

word_part: Lit_Chars | Lit_Other | Glob_Star | char_class
word: word_part*
array_literal: (
  '@[' [WS_Space] [word] (WS_Space word)* [WS_Space] Op_RBracket
)

char_class_part: Lit_Chars  # TODO: letters, escaped, and ranges
char_class: (
  Op_LBracket [WS_Space]
  [char_class_part] (WS_Space char_class_part)*
  [WS_Space] Op_RBracket
)

regex_part: Expr_Name | dq_string | char_class
# empty regex invalid for now
regex_literal: '$/' regex_part+ Arith_Slash

# line continuation is ignored by lexer
dq_part: (
  Lit_EscapedChar | Lit_Chars | Lit_Other | VSub_DollarName | VSub_Number |
  command_sub | expr_sub | var_sub
)
dq_string: Left_DoubleQuote dq_part* Right_DoubleQuote

command_sub: (
  '$[' [WS_Space] [word] (WS_Space word)* [WS_Space] Op_RBracket
)

expr_sub: '$(' expr Op_RParen

var_sub: '${' expr Op_RBrace


expr: term (('+'|'-') term)*

# integer division is 'div'

term: factor (('*'|'/'|'div'|'%') factor)*

factor: ('+'|'-'|'~') factor | power

power: atom trailer* ['^' factor]

trailer: '(' [arglist] ')' | '[' subscriptlist ']'

# note: calling an array literal is a runtime error
atom: (
  NAME | NUMBER | array_literal | regex_literal |
  dq_string |
  command_sub | var_sub | expr_sub
)

# Trailing commas are allowed!
arglist: expr (',' expr)* [',']
subscriptlist: expr (',' expr)* [',']


test: or_test ['if' or_test 'else' test] | func_literal
or_test: and_test ('or' and_test)*
and_test: not_test ('and' not_test)*
not_test: 'not' not_test | comparison
comparison: expr (comp_op expr)*
comp_op: '<'|'>'|'=='|'>='|'<='|'!='|'in'|'not' 'in'|'is'|'is' 'not'

func_literal: 'func'
